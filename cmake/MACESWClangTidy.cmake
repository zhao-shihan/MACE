find_program(CLANG_TIDY_EXE clang-tidy)
if(NOT CLANG_TIDY_EXE)
    set(MACESW_CLANG_TIDY OFF)
    message(NOTICE "***Notice: clang-tidy not found. For the time turning off MACESW_CLANG_TIDY")
endif()
if(NOT CMAKE_CXX_COMPILER_ID MATCHES "^(GNU|Clang)$")
    set(MACESW_CLANG_TIDY OFF)
    message(NOTICE "***Notice: Not using GCC or LLVM Clang. For the time turning off MACESW_CLANG_TIDY")
endif()

if(MACESW_CLANG_TIDY)
    set(MACESW_CLANG_TIDY_FULL_COMMAND
            ${CLANG_TIDY_EXE}
                --config-file=${MACESW_PROJECT_ROOT_DIR}/.clang-tidy
                --header-filter=${MACESW_PROJECT_SOURCE_DIR}/.*)
    if(MACESW_CLANG_FORMAT_WERROR)
        list(APPEND MACESW_CLANG_TIDY_FULL_COMMAND --warnings-as-errors=*)
    endif()
    set(CMAKE_CXX_CLANG_TIDY ${MACESW_CLANG_TIDY_FULL_COMMAND})
    if(MACESW_CLANG_TIDY_WERROR)
        message(STATUS "MACESW source code will be analysed by ${CLANG_TIDY_EXE} (warnings as errors)")
    else()
        message(STATUS "MACESW source code will be analysed by ${CLANG_TIDY_EXE}")
    endif()
    if(MACESW_UNITY_BUILD)
        message(WARNING "clang-tidy may generate false positives when using unity build. Try disable unity build (MACESW_UNITY_BUILD=OFF) to perform code analysis")
    endif()
else()
    message(NOTICE "***Notice: MACESW source code will not be analysed by clang-tidy")
endif()
